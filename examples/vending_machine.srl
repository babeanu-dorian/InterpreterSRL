// A demo for a vending machine program.

import: "stdlib.srl".

signal_domain : MENU, COIN, TEA, COFFEE, COLA, WATER.

shared_data: credit.

lock(credit,
	credit = 0;
	print("Vending machine demo. Commands:\nMENU - display product list with prices\nCOIN - increment credit\n\n");
);

thread(
	loop(true,
		when(MENU,
			print("\nMenu:\nTEA     2$\nCOFFEE  3$\nCOLA    2$\nWATER   1$\n\n");
		);
		pause();
	);
);

thread(
	loop(true,
		when(COIN,
			lock(credit,
				credit = credit + 1;
				print("\nCurrent credit: " + credit +"\n\n");
			);
		);
		pause();
	);
);

let orderListener(signal, name, price) = thread(
	loop(true,
		when(signal,
			lock(credit,
				if(price <= credit,
					credit = credit - price;
					print("\nYour order: " + name + "\nRemaining credit: " + credit + "\n\n");
					, //else:
					print("\nInsufficient credit: " + credit + "\nPrice of " + name + ": " + price + "\n\n");
				);
			);
		);
		pause();
	);
);

orderListener(TEA, "Tea", 2);
orderListener(COFFEE, "Coffee", 3);
orderListener(COLA, "Cola", 2);
orderListener(WATER, "Water", 1);